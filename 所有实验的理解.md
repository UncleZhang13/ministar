##### 实验中不太重要的代码

###### 按键消抖 btn_deb.v

```verilog
`define UD #1
module btn_deb#(
    parameter                  BTN_WIDTH = 4'd8,
    parameter                  MS_20 = 20'd240000
)
(
    input                      clk,//27MHz
    input      [BTN_WIDTH-1:0] btn_in,
    
    output reg [BTN_WIDTH-1:0] btn_deb
);

//==================================================================================
    reg [19:0] time_cnt= 20'd0;
    always@(posedge clk)
    begin
        if(time_cnt == MS_20 - 1'b1)
            time_cnt <= 20'd0;
        else
            time_cnt <= time_cnt + 1'd1;
    end
    
    always @(posedge clk)
    begin
        if(time_cnt == MS_20 - 1'b1)
            btn_deb <= btn_in;
    end

endmodule
```

###### 时钟分频 div_clk.v

分频得到100khz的时钟，即10ms

```verilog
`define UD #1
module div_clk
(
    input clk,//27MHz
    output  clk_1khz
);
    //times =27000 : 37ns * 27000 = 1ms -> 1khz;
    reg [14:0]cnt;
    always @(posedge clk)
    begin
        if(cnt == 15'd26999)
            cnt<= `UD 15'd0;
        else 
            cnt <= `UD cnt + 1'b1;
    end  
    
    reg flag=1'b0;
    always @(posedge clk)
    begin
        if(cnt == 15'd13499)
            flag <= `UD 1'b1;
        else if(cnt == 15'd26999)
            flag <= `UD 1'b0;
    end 
    assign clk_1khz = flag;

endmodule
```

###### 数码管显示 seq_control.v

```verilog
`define UD #1
module seq_control
(
	input [1:0]sel,
	input [3:0]key,
	output reg [3:0]dig,
	output reg [7:0]smg
);
/*===================================================
					位选择映射
===================================================*/
always @(*)
begin
	case(sel)
		2'd0   :dig = 4'b0001;
		2'd1   :dig = 4'b0010;
		2'd2   :dig = 4'b0100;
		2'd3   :dig = 4'b1000;
		default:dig = 4'b0000;
	endcase
end
//===========================================
//IO_LOC "smg[0]" 1 -> G;
    //IO_LOC "smg[1]" 39 -> F;
    //IO_LOC "smg[2]" 40  -> E;
    //IO_LOC "smg[3]" 41  -> D;
    //IO_LOC "smg[4]" 43 -> C;
    //IO_LOC "smg[5]" 45 -> B;
    //IO_LOC "smg[6]" 47 -> A;
    //IO_LOC "smg[7]" 2  -> P;
//===========================================
// 0 1 2 3 4 5 6 7  
// G F E D C B A P
//共阳极数码管，为0有效，即点亮
//===========================================
always @(*)
begin
	case(key)
		4'd0   :smg = 8'b1000_0001;//"0"  8'b1000_0001
		4'd1   :smg = 8'b1100_1111;//"1"  8'b1100_1111
		4'd2   :smg = 8'b1001_0010;//"2"  8'b1001_0010
		4'd3   :smg = 8'b1000_0110;//"3"  8'b1000_0110
		4'd4   :smg = 8'b1100_1100;//"4"  8'b1100_1100
		4'd5   :smg = 8'b1010_0100;//"5"  8'b1010_0100
		4'd6   :smg = 8'b1010_0000;//"6"  8'b1010_0000
		4'd7   :smg = 8'b1000_1111;//"7"  8'b1000_1111
		4'd8   :smg = 8'b1000_0000;//"8"  8'b1000_0000
		4'd9   :smg = 8'b1000_0100;//"9"  8'b1000_0100
		4'd10  :smg = 8'b1100_0000;//"9"  8'b1000_0100
		default:smg = 8'b0000_0000;		
	endcase
end 
endmodule
```

###### 按键计数模块 key_cnt.v

```verilog
`define UD #1
module key_cnt
(
	input clk,
	input key,
	output reg [3:0]key_times
);

reg key_reg;
always @(posedge clk)
begin
	key_reg <= `UD key;
end 

always @(posedge clk )
begin
    if(key_reg&&~key&&key_times==4'd15) //4'd15可以更改为其他次数
		key_times <=`UD 4'd0;
	else if(key_reg&&~key)
		key_times <=`UD key_times + 1'b1;
end 

endmodule
```

###### 数码管选择 top.v

```verilog
    // display by Digital Tube
    always @(posedge clk_1khz)
    begin
        if(sel==2'b00)
            dig <= `UD dig0;
        else if(sel==2'b01)
            dig <= `UD dig1;
        else if(sel==2'b10)
            dig <= `UD dig2;
        else if(sel==2'b11)
            dig <= `UD dig3;
    end 
    
    always @(posedge clk_1khz)
    begin
        if(sel==2'b00)
            smg <= `UD smg0;
        else if(sel==2'b01)
            smg <= `UD smg1;
        else if(sel==2'b10)
            smg <= `UD smg2;
        else if(sel==2'b11)
            smg <= `UD smg3;
    end 
```

上升沿检测

```verilog
   always @(posedge clk)
    begin
        key_det_led_1d <= `UD key_det_led; 
    end 
    
    always @(posedge clk)
    begin
        if(~key_det_led_1d && key_det_led) //检测按键触发后才进入 按下key
            det_en <= `UD 1'b1; 
    end 
```

##### exp3_key_led

###### led状态控制 led.v

```verilog
    always @(posedge clk)
    begin
        if(led_light_cnt == 25'd1349_9999)//0.5s 周期
        begin
            case(ctrl)
                2'd0 :  //从高位到低位的led流水灯
                begin
                    if(ctrl_1d != ctrl)
                        led_status <= `UD 8'b0111_1111;
                    else
                        led_status <= `UD {led_status[0],led_status[7:1]};
                end
                2'd1 :  //从低位到高位的led流水灯
                begin
                    if(ctrl_1d != ctrl)
                        led_status <= `UD 8'b1111_1110;
                    else
                        led_status <= `UD {led_status[6:0],led_status[7]};
                end
                2'd2 :  //从高位到低位暗灯流水
                begin
                    if(ctrl_1d != ctrl || led_status == 8'b1111_1111)
                        led_status <= `UD 8'b1000_0000;
                    else
                        led_status <= `UD {led_status[0],led_status[7:1]};
                end
            endcase
        end
    end
```

###### 按键状态判断 key_ctl.v

```verilog
    always @(posedge clk)
    begin
        if(~btn_deb & btn_deb_1d) //上升沿检测Key
        begin
            if(key_push_cnt == 2'd2)
                key_push_cnt <= `UD 2'd0;
            else
                key_push_cnt <= `UD key_push_cnt + 2'd1;
        end
    end
```



##### exp4_seg_display

通过key_cnt 计算按键次数然后传送到 seq_control 即可实现功能

##### exp5_seq_det

###### 序列判断 seq_det.v

```verilog

    always @(posedge clk)
    begin
        if(key_det_led && det_en)
        begin
            flag[0] <= `UD (bm[3:2]==key_in_led); //识别SW4 SW3
            flag[1] <= `UD (bm[2:1]==key_in_led); //识别SW3 SW2
            flag[2] <= `UD (bm[1:0]==key_in_led); //识别SW2 SW1
        end
    end
    
    always @(posedge clk)
    begin
        if(~key_det_led && key_det_led_1d)//检测结束统计结果 再次按下key4
            data <= `UD flag[2] + flag[1] + flag[0];  //所有识别结果显示在smg0上
    end 
```

但是在现实实现中存在bug、不知道是不是插线问题

```verilog
    always @(posedge clk)
    begin
        key_in_led <= `UD ~key_flag;//LED高电平熄灭
    end
    
    //seq_data
    always @(posedge clk)
    begin
        if(key_8_flag)
            seq_data <= `UD ~key_in_led;
    end
```

注意在 key_control.v 中输出序列和led显示是取反的因为LED是高电平熄灭

##### exp6_lock

```verilog
assign ctrl = {key2_push_cnt,key1_push_cnt};
```

key_control.v 中这句话实现Key1和 Key2的结果输出

通过compare.v 实现Key与switch密码的匹配，输出com_result

###### seq_display.v

led显示控制

```verilog
    //数码管显示荣控制
    reg [3:0] key0_cnt=4'd0,key1_cnt=4'd0,key2_cnt=4'd0,key3_cnt=4'd0;
    always @(posedge clk)
    begin
        if(seq_status) //输入的密码状态
        begin
            if(com_result) //如果正确显示8888，否则显示7777
            begin
                key0_cnt <= `UD 4'd8;
                key1_cnt <= `UD 4'd8;
                key2_cnt <= `UD 4'd8;
                key3_cnt <= `UD 4'd8;
            end
            else
            begin
                key0_cnt <= `UD 4'd7;
                key1_cnt <= `UD 4'd7;
                key2_cnt <= `UD 4'd7;
                key3_cnt <= `UD 4'd7;
            end
        end
        else
        begin
            key0_cnt <= `UD {2'd0,ctrl[1:0]};
            key1_cnt <= `UD {2'd0,ctrl[3:2]};
            key2_cnt <= `UD 4'd0;
            key3_cnt <= `UD 4'd0;
        end
    end
```

##### exp7_watch

###### 产生位选时候的闪烁

seq_control.v 中control_dig(Key1) 控制数码管位选

```verilog
begin if(sec_en) dig = (4'b0001 );else dig = (4'b0000); end //产生闪烁功能
```

配合watch_data_gen.v 

```verilog
//每个1s闪烁一次
always @(posedge clk)
begin
	if(!rstn)
		second_led <= `UD 1'b0;
    if(second_cnt==(CLK_FRE>>1)-1'b1) // 0.5s
		second_led <= `UD 1'b1;
	else if(second_cnt==CLK_FRE-1'b1)
		second_led <= `UD 1'b0;
end 
```



时钟的特殊时间点

59s, 59min, 9/19h, 23h

四个数码管分别控制hour_h, hour_l, min_h, min_l



###### 控制hour_h, min_h, min_l

以控制min_l为例

校准模式下

```verilog
//控制分钟低位
always @(posedge clk)
begin
	if(key_cnt==3'd0)//校准前将分钟低位和输出值保持一致
		minutes_l_fix <= `UD minutes_l;
	else if(!key_out[1] && key_out_reg[1] && key_cnt==3'd1 && minutes_l_fix == 4'd9)//当处于分钟校准状态时，按下"+"按键,且此时校准值已经为9时，再按下按键，则校准值变为0
		minutes_l_fix <= `UD 4'd0;//"+"
	else if(!key_out[2] && key_out_reg[2] && key_cnt==3'd1 && minutes_l_fix == 4'd0)//当处于分钟校准状态时，按下"-"按键,且此时校准值已经为0时，再按下按键，则校准值变为9
		minutes_l_fix <= `UD 4'd9;//"-"
	else if(!key_out[1] && key_out_reg[1] && key_cnt==3'd1)//当处于分钟低位校准状态时，按下"+"按键,校准数值加1；
		minutes_l_fix <= `UD minutes_l_fix + 1'b1;//"+"
	else if(!key_out[2] && key_out_reg[2] && key_cnt==3'd1)//当处于分钟低位校准状态时，按下"-"按键,校准数值减1；
		minutes_l_fix <= `UD minutes_l_fix - 1'b1;	//"-"	
end 
```

hour_h 的校准值为2， min_h的校准值为5



正常模式下

```verilog
//minutes_l gen 
always @(posedge clk)
begin
	if(!rstn)//初始值为0
		minutes_l <= `UD 4'd0;
	else if(key_cnt==3'd1)//校准时，分钟低位为校准值
		minutes_l <= `UD minutes_l_fix;
	else if(second_cnt==CLK_FRE-1'b1 && second_flag==6'd59 && minutes_l==4'd9)//9分59秒产生进位，低位赋值为0
		minutes_l <= `UD 4'd0;	
	else if(second_cnt==CLK_FRE-1'b1 && second_flag==6'd59)//60秒产生分钟的低位进位
		minutes_l <= `UD minutes_l +1'b1;		
end 
```

min_h的进位值为9min59s， 归零值为59min59s

hour_h的进位值为59min59s,  归零值为23h59min59s



###### 控制hour_l

控制这个麻烦一点，不仅要判断9/19，还要判断23

校准模式下

```verilog
//控制时钟低位
always @(posedge clk)
begin
	if(key_cnt!=3'd3 )//校准前数据赋值为0
		hour_l_fix <= `UD 4'd0;
	else if(!key_out[1] && key_out_reg[1] && key_cnt==3'd3 && hour_h_o != 4'd2 && hour_l_fix==4'd9)//当处于校准状态时，按下"+"按键,且此时小时的高位不为2且校准值已经为9时，再按下按键，则校准值变为0
		hour_l_fix <= `UD 4'd0;//"+"   当hour ！= 20+h 时
	else if(!key_out[1] && key_out_reg[1] && key_cnt==3'd3 && hour_h_o == 4'd2 && hour_l_fix==4'd3)//当处于校准状态时，按下"+"按键,且此时小时的高位为2且校准值已经为3时，再按下按键，则校准值变为0
		hour_l_fix <= `UD 4'd0;//"+"   当hour == 20+h 时	
	else if(!key_out[2] && key_out_reg[2] && key_cnt==3'd3 && hour_h_o != 4'd2 && hour_l_fix==4'd0)//当处于校准状态时，按下"-"按键,且此时小时的高位不为2时，校准位为0，再按下按键，则校准值变为,9
		hour_l_fix <= `UD 4'd9;//"-"   当hour ！= 20+h 时
	else if(!key_out[2] && key_out_reg[2] && key_cnt==3'd3 && hour_h_o == 4'd2 && hour_l_fix==4'd0)//当处于校准状态时，按下"-"按键,且此时小时的高位为2时，校准位为0，再按下按键，则校准值变为3
		hour_l_fix <= `UD 4'd3;//"-"   当hour == 20+h 时			
	else if(!key_out[1] && key_out_reg[1] && key_cnt==3'd3)//当处于校准状态时，按下"+"按键,按下"+"按键,校准数值加1；
		hour_l_fix <= `UD hour_l_fix + 1'b1;//"+"
	else if(!key_out[2] && key_out_reg[2] && key_cnt==3'd3)//当处于校准状态时，按下"-"按键,校准数值减1；
		hour_l_fix <= `UD hour_l_fix - 1'b1;		
end 
```



正常模式下

```verilog
always @(posedge clk)
begin
	if(!rstn)
		hour_l <= `UD 4'd0;
	else if(key_cnt==3'd3)
		hour_l <= `UD hour_l_fix;
	else if(hour_h!=4'd2 && hour_l==4'd9 && second_cnt==CLK_FRE-1'b1 && second_flag==6'd59 && minutes_h==4'd5 && minutes_l==4'd9 )//9:59:59或者19:59:59，下一秒hour_l为0；
		hour_l <= `UD 4'd0;
	else if(hour_h==4'd2 && hour_l==4'd3 && second_cnt==CLK_FRE-1'b1 && second_flag==6'd59  && minutes_h==4'd5 && minutes_l==4'd9 )//23:59:59;hour_l为0；
		hour_l <= `UD 4'd0;			
	else if(second_cnt==CLK_FRE-1'b1 && minutes_h==4'd5 && minutes_l==4'd9 && second_flag==6'd59)//XX:59:59;hour_l加1；
		hour_l <= `UD hour_l +1'b1;			
end 
```

我觉得中间的两个else if 可以用一个||即可代替



##### exp8_frequency_test

###### 时钟的产生

```verilog
reg [23:0] clk_gen;
always @(posedge clk)
begin
	clk_gen <=`UD clk_gen + 1'b1;
end 

wire  freq_gen;
assign  freq_gen = clk_gen[23-key_times];
```

clk_gen计数，通过控制第几位开始变化从而控制频率

###### 测试时间的控制

```verilog
reg test_flag;
reg [1:0]flag_cnt=2'd0;
always @(posedge clk_1hz) 
begin
	flag_cnt <=`UD flag_cnt + 1'b1; //3s刷新
end 

always @(*)
begin
	if(flag_cnt==2'd0) //3s刷新
		test_flag = 1'b1;
	else 
		test_flag = 1'b0;
end 

reg test_flag_reg;

always @(posedge clk)
begin
	test_flag_reg <=`UD test_flag;
end 

wire test_start;
assign test_start = test_flag && test_flag_reg==1'b0;  //确保每次测试的时间相同

reg freq_gen_reg;
always @(posedge clk)
begin
	freq_gen_reg <= `UD freq_gen;
end 

wire freq_risedge;
assign freq_risedge = !freq_gen_reg && freq_gen;
```



###### 数码管控制

溢出标志声明

```verilog
wire seg0_carry;
wire seg1_carry;
wire seg2_carry;
wire seg3_carry;
//test_flag是一个脉冲为1s的测试使能信号，freq_risedge为待测试信号上升沿
assign seg0_carry = (seg0 == 4'd9) && freq_risedge && test_flag; //个位溢出
assign seg1_carry = (seg1 == 4'd9) && seg0_carry; //十位溢出
assign seg2_carry = (seg2 == 4'd9) && seg1_carry; //百位溢出
assign seg3_carry = (seg3 == 4'd9) && seg2_carry; //千位溢出

```



数码管控制

以个位为例

```verilog
always @(posedge clk)     //频率的个位
begin
	if(seg3_carry)        //当我们测量达到最大值时，将赋值为9
		seg0 <= `UD 4'd9;
	else if(seg0_carry)   //溢出
		seg0 <= `UD 4'd0;
	else if(freq_risedge && test_flag)//
		seg0 <= `UD seg0 + 1'b1;
	else if(test_start)    //每次测量前将数码管赋值为0
		seg0 <= `UD 4'd0;
end 
```

